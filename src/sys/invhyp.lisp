;;;; invhype.lisp
(in-package #:chenyi.sys)

(declaim (inline %acosh/f64 %asinh/f64 %atanh/f64))

(defun %acosh/f64 (x)
  (declare (type double-float x)
           (optimize speed (space 0) (safety 0)))
  (cond ((> x (/ 1d0 +GSL-SQRT-DBL-EPSILON+))
         (+ (log x) +ln2+))
        ((> x 2d0)
         (log (- (* 2d0 x)
                 (/ 1d0 (+ (sqrt (- (* x x) 1d0)) x)))))
        ((> x 1d0)
         (let ((tmp 0d0))
           (declare (type double-float tmp)
                    (dynamic-extent tmp))
           (setq tmp (- x 1d0))
           (log1p (+ tmp (sqrt (+ (* 2d0 tmp) (* tmp tmp)))))))
        ((= x 1d0) 0)
        (t nan)))

(defun acosh (x)
  "This function computes the hyperbolic arc cosine of x. It provides an alternative to the standard math function cl:acosh when x is not a Complex number."
  (unless (numberp x)
    (error 'domain-error :operation "acosh" :expect "Number"))
  (typecase x
    (double-float (%acosh/f64 x))
    (real (%acosh/f64 (float x 0d0)))
    (t (let ((r (realpart x))
             (i (imagpart x)))
         (ensure-double-float (r i)
           (cl:acosh (complex r i)))))))

(define-compiler-macro acosh (&whole form &environment env x)
  (cond ((and (constantp x env) (numberp x))
         (acosh x))
        (t form)))

(defun %asinh/f64 (x)
  (declare (type double-float x)
           (optimize speed (space 0) (safety 0)))
  (let ((a 0d0) (s -1))
    (declare (type double-float a)
             (type fixnum s)
             (dynamic-extent a s))
    (setq a (abs x)
          s (if (minusp x) -1 1))
    (cond ((> a (/ 1d0 +GSL-SQRT-DBL-EPSILON+))
           (* s (+ (log a) +ln2+)))
          ((> a 2d0)
           (* s (log (+ (* 2d0 a)
                        (/ 1d0 (+ a (sqrt (+ (* a a) 1))))))))
          ((> a +GSL-SQRT-DBL-EPSILON+)
           (let ((a2 0d0))
             (declare (type double-float a2)
                      (dynamic-extent a2))
             (setq a2 (* a a))
             (* s (log1p (+ a (/ a2 (+ 1 (sqrt (+ a2 1)))))))))
          (t x))))

(defun asinh (x)
  "This function computes the hyperbolic arc sine of x. It provides an alternative to the standard math function cl:asinh when x is not a Complex number."
  (unless (numberp x)
    (error 'domain-error :operation "asinh" :expect "Number"))
  (typecase x
    (double-float (%asinh/f64 x))
    (real (%asinh/f64 (float x 0d0)))
    (t (let ((r (realpart x))
             (i (imagpart x)))
         (ensure-double-float (r i)
           (cl:asinh (complex r i)))))))

(define-compiler-macro asinh (&whole form &environment env x)
  (cond ((and (constantp x env) (numberp x))
         (asinh x))
        (t form)))

(defun %atanh/f64 (x)
  (declare (type double-float x)
           (optimize speed (space 0) (safety 0)))
  (let ((a 0d0) (s -1))
    (declare (type double-float a)
             (type fixnum s)
             (dynamic-extent a s))
    (setq a (abs x)
          s (if (minusp x) -1 1))
    (cond ((> a 1d0) nan)
          ((= a 1d0)
           (if (minusp x) -inf inf))
          ((>= a 0.5d0)
           (* s 0.5d0 (log1p (/ (* 2d0 a)
                                (- 1d0 a)))))
          ((> a +GSL-DBL-EPSILON+)
           (* s 0.5d0 (log1p (+ (* 2d0 a)
                                (/ (* 2d0 a a)
                                   (- 1d0 a))))))
          (t x))))

(defun atanh (x)
  "This function computes the hyperbolic arc tangent of x. It provides an alternative to the standard math function cl:atanh when x is not a Complex number."
  (unless (numberp x)
    (error 'domain-error :operation "atanh" :expect "Number"))
  (typecase x
    (double-float (%atanh/f64 x))
    (real (%atanh/f64 (float x 0d0)))
    (t (let ((r (realpart x))
             (i (imagpart x)))
         (ensure-double-float (r i)
           (cl:atanh (complex r i)))))))

(define-compiler-macro atanh (&whole form &environment env x)
  (cond ((and (constantp x env) (numberp x))
         (atanh x))
        (t form)))
